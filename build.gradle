apply plugin: 'com.android.library'

def USE_PACKAGED_NATIVE_LIBRARIES = false

android {
    compileSdkVersion 28
    ndkVersion "21.0.6113669"

    defaultConfig {
        minSdkVersion 25
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=ON"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    if (!USE_PACKAGED_NATIVE_LIBRARIES) {
        externalNativeBuild {
            cmake {
                buildStagingDirectory ".cxx"
                path "CMakeLists.txt"
            }
        }
        sourceSets.main.jniLibs.srcDirs = []
    } else {
        sourceSets.main.jniLibs.srcDirs "lib/"
    }
}

if (!USE_PACKAGED_NATIVE_LIBRARIES) {
    task copyNativeLibrariesToLib(type: Copy) {
        for (abi in ["arm64-v8a", "armeabi-v7a"]) {
            from(".cxx/cmake/debug/" + abi + "/lib") {
                include(abi + "/*.so")
            }
        }

        into "./lib"
    }
}

repositories {
    flatDir {
        dirs '../libs'
    }
    maven { url = "https://dl.bintray.com/first-tech-challenge/ftcsdk/" }
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':FtcRobotController')
    implementation 'org.firstinspires.ftc:RobotCore:6.0.1'
    implementation 'org.firstinspires.ftc:Hardware:6.0.1'
    implementation 'org.firstinspires.ftc:FtcCommon:6.0.1'
    implementation "org.apache.commons:commons-lang3:3.8.1"
    //implementation 'com.android.support:appcompat-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'org.jetbrains:annotations:15.0'

}
