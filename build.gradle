apply plugin: "com.android.library"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 25
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 28
        versionName "0.4"
        // version code is the version name with dots and leading zeros removed
        versionCode android.defaultConfig.versionName.replaceAll("(\\.)|(^0+)", "").toInteger()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ARM_NEON=TRUE"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            buildStagingDirectory ".cxx"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task copyNativeLibrariesToLib(type: Copy) {
    for (abi in ["arm64-v8a", "armeabi-v7a"]) {
        from(".cxx/cmake/debug/" + abi + "/lib") {
            include(abi + "/*.so")
        }
    }

    into "./lib"
}

repositories {
    flatDir {
        dirs '../libs'
    }
    maven { url = "https://dl.bintray.com/first-tech-challenge/ftcsdk/" }
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //implementation project(':FtcRobotController')
    implementation 'org.firstinspires.ftc:RobotCore:7.0.0'
    implementation 'org.firstinspires.ftc:Hardware:7.0.0'
    implementation 'org.firstinspires.ftc:FtcCommon:7.0.0'
    implementation 'org.apache.commons:commons-lang3:3.9'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'org.jetbrains:annotations:20.1.0'
}

apply from: "publish.gradle"
// uncomment to enable publishing to maven central
//apply from: "publishMavenCentral.gradle"
